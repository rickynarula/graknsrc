insert

# Resources
gender sub resource datatype string;
date sub resource datatype string;
birth-date sub date;
death-date sub date;
name sub resource datatype string;
firstname sub name;
middlename sub name;
surname sub name;
function sub resource datatype string;
age sub resource datatype long;
identifier sub resource datatype string;
child-identifier sub identifier;
parent-identifier sub identifier;
# confidence level: low-mid-high
confidence sub resource datatype string;
document-location sub resource datatype string;
document-type sub resource datatype string;
notes sub resource datatype string;
# This is needed otherwise reasoner complains about the missing resource if it has not been persisted
degree sub resource datatype long;


# Entities

person sub entity
	plays-role protagonist
	plays-role spouse1
	plays-role spouse2
	plays-role wife
	plays-role husband
	plays-role granddaughter
	plays-role grandson
	plays-role father-in-law
	plays-role mother-in-law
	plays-role daughter-in-law
	plays-role son-in-law
	plays-role son
	plays-role daughter
	plays-role parent
	plays-role child
	plays-role sibling1
	plays-role sibling2
	plays-role grandparent
	plays-role grandchild
	plays-role cousin1
	plays-role cousin2
	plays-role child-in-law
	plays-role parent-in-law
	plays-role mother
	plays-role father
	plays-role grandmother
	plays-role grandfather
	has-resource age
	has-resource gender
	has-resource birth-date
	has-resource death-date
	has-resource identifier
	has-resource firstname
	has-resource middlename
	has-resource surname;

event sub entity
	is-abstract
	plays-role conclusion
	plays-role happening
	has-resource degree
	has-resource confidence
	has-resource date
	has-resource identifier
	has-resource notes;

birth sub event
	has-resource firstname
	has-resource middlename
	has-resource surname
	has-resource gender
	has-resource birth-date;

death sub event
	has-resource death-date
	has-resource age;

wedding sub event;

document sub entity
	plays-role evidence
	has-resource identifier
	has-resource document-location
	has-resource document-type
	has-resource notes;

# Relations and Roles

event-protagonist sub relation
	has-role happening
	has-role protagonist
	has-resource function;

happening sub role;
protagonist sub role;

relatives sub relation
	is-abstract;

marriage sub relatives
	has-role spouse1
	has-role spouse2
	has-role husband
	has-role wife
	has-resource date;

spouse sub role is-abstract;
generic-spouse sub spouse is-abstract;
spouse1 sub generic-spouse;
spouse2 sub generic-spouse;
husband sub spouse;
wife sub spouse;

parentship sub relatives
	has-role parent
	has-role mother
	has-role father
	has-role child
	has-role son
	has-role daughter;

parent sub role;
mother sub parent;
father sub parent;
child sub role;
son sub child;
daughter sub child;

siblings sub relatives
	has-role sibling1
	has-role sibling2;

sibling sub role is-abstract;
sibling1 sub sibling;
sibling2 sub sibling;

grandparentship sub relatives
	has-role grandparent
	has-role grandfather
	has-role grandmother
	has-role grandchild
	has-role granddaughter
	has-role grandson;

grandparent sub role;
grandfather sub grandparent;
grandmother sub grandparent;
grandchild sub role;
grandson sub grandchild;
granddaughter sub grandchild;

cousins sub relatives
	has-role cousin1
	has-role cousin2;

cousin sub role is-abstract;
cousin1 sub cousin;
cousin2 sub cousin;

in-laws sub relatives
	has-role child-in-law
	has-role son-in-law
	has-role daughter-in-law
	has-role parent-in-law
	has-role mother-in-law
	has-role father-in-law;

child-in-law sub role;
son-in-law sub child-in-law;
daughter-in-law sub child-in-law;
parent-in-law sub role;
mother-in-law sub parent-in-law;
father-in-law sub parent-in-law;

conclusion-evidence sub relation
	has-role evidence
	has-role conclusion;

evidence sub role;
conclusion sub role;