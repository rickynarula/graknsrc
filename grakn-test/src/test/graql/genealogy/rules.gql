insert

#############################################################################
#                                                                           #
#         Rules to make events with many evidences high confidence          #
#                                                                           #
#############################################################################

#$makeHighDegreeEventsHighConfidence isa inference-rule
#lhs
#{$e isa event has degree > 2;
#}
#rhs
#{$f has confidence "high";};


#############################################################################
#                                                                           #
# Rules to add personal data from high confidence events to person entities #
#                                                                           #
#############################################################################

 $materializePeopleNames isa inference-rule
 lhs
 {$b isa birth has confidence "high" has firstname $name;
 $rel (happening: $b, protagonist: $p) isa event-protagonist;
 $rel has event-role "newborn";}
 rhs
 {$p has firstname $name;};

 $materializePeopleMiddleNames isa inference-rule
 lhs
 {$b isa birth has confidence "high" has middlename $name;
 $rel (happening: $b, protagonist: $p) isa event-protagonist;
 $rel has event-role "newborn";}
 rhs
 {$p has middlename $name;};

 $materializePeopleSurnames isa inference-rule
 lhs
 {$b isa birth has confidence "high" has surname $name;
 $rel (happening: $b, protagonist: $p) isa event-protagonist;
 $rel has event-role "newborn";}
 rhs
 {$p has surname $name;};

 $materializePeopleGender isa inference-rule
 lhs
 {$b isa birth has confidence "high" has gender $gender;
 $rel (happening: $b, protagonist: $p) isa event-protagonist;
 $rel has event-role "newborn";}
 rhs
 {$p has gender $gender;};

 $materializePeopleBirthDates isa inference-rule
 lhs
 {$b isa birth has confidence "high" has birth-date $date;
 $rel (happening: $b, protagonist: $p) isa event-protagonist;
 $rel has event-role "newborn";}
 rhs
 {$p has birth-date $date;};

 $materializePeopleDeathDates isa inference-rule
 lhs
 {$b isa death has confidence "high" has death-date $date;
 $rel (happening: $b, protagonist: $p);
 $rel isa event-protagonist has event-role "deceased";}
 rhs
 {$p has death-date $date;};

#############################################################################
#                                                                           #
#     Rules to retrieve parentships from high confidence birth events       #
#                                                                           #
#############################################################################

$materializeParentChildRelations isa inference-rule
lhs
{
$b isa birth has confidence "high";
$rel1 (happening: $b, protagonist: $p) isa event-protagonist;
$rel1 has event-role "parent";
$rel2 (happening: $b, protagonist: $c) isa event-protagonist;
$rel2 has event-role "newborn";}
rhs
{(parent: $p, child: $c) isa parentship;};

#############################################################################
#                                                                           #
#     Rules to retrieve marriages from high confidence wedding events       #
#                                                                           #
#############################################################################

$materializeMarriagesFromWeddings isa inference-rule
lhs
{$w isa wedding has confidence "high";
$rel1 (happening: $w, protagonist: $s1) isa event-protagonist;
$rel1 has event-role "spouse";
$rel2 (happening: $w, protagonist: $s2) isa event-protagonist;
$rel2 has event-role "spouse";
$s1 != $s2;
}
rhs
{(spouse1: $s1, spouse2: $s2) isa marriage;};


#############################################################################
#                                                                           #
#     Rules to genderize relationships      #
#                                                                           #
#############################################################################

isa inference-rule
lhs
{(grandparent: $x, grandchild: $y) isa grandparentship;
$x has gender "female";
}
rhs
{(grandmother: $x) isa grandparentship;};

isa inference-rule
lhs
{(grandparent: $x, grandchild: $y) isa grandparentship;
$x has gender "male";
}
rhs
{(grandfather: $x) isa grandparentship;};

isa inference-rule
lhs
{(grandchild: $x, grandparent: $y) isa grandparentship;
$x has gender "male";
}
rhs
{(grandson: $x) isa grandparentship;};

isa inference-rule
lhs
{(grandchild: $x, grandparent: $y) isa grandparentship;
$x has gender "female";
}
rhs
{(granddaughter: $x) isa grandparentship;};

isa inference-rule
lhs
{(parent: $x, child: $y) isa parentship;
$x has gender "female";
}
rhs
{(mother: $x) isa parentship;};

isa inference-rule
lhs
{(parent: $x, child: $y) isa parentship;
$x has gender "male";
}
rhs
{(father: $x) isa parentship;};

isa inference-rule
lhs
{(child: $x, parent: $y) isa parentship;
$x has gender "female";
}
rhs
{(daughter: $x) isa parentship;};

isa inference-rule
lhs
{(child: $x, parent: $y) isa parentship;
$x has gender "male";
}
rhs
{(son: $x) isa parentship;};

isa inference-rule
lhs
{($x, $y) isa marriage;
$x has gender "female";
$y has gender "male";}
rhs
{(wife: $x, husband: $y) isa marriage;};

isa inference-rule
lhs
{(parent-in-law: $x, child-in-law: $y) isa in-laws;
$x has gender "male";
}
rhs
{(father-in-law: $x) isa in-laws;};

isa inference-rule
lhs
{(parent-in-law: $x, child-in-law: $y) isa in-laws;
$x has gender "female";
}
rhs
{(mother-in-law: $x) isa in-laws;};

isa inference-rule
lhs
{(child-in-law: $x, parent-in-law: $y) isa in-laws;
$x has gender "male";
}
rhs
{(son-in-law: $x) isa in-laws;};

isa inference-rule
lhs
{(child-in-law: $x, parent-in-law: $y) isa in-laws;
$x has gender "female";
}
rhs
{(daughter-in-law: $x) isa in-laws;};

#############################################################################
#                                                                           #
#     Rules to infer kinships
#                                                                           #
#############################################################################

$parentsOfParentsAreGrandparents isa inference-rule
lhs
{(parent:$p, child: $gc) isa parentship;
(parent: $gp, child: $p) isa parentship;
}
rhs
{(grandparent: $gp, grandchild: $gc) isa grandparentship;};

$peopleWithSameParentsAreSiblings isa inference-rule
lhs
{(parent: $m, child: $x) isa parentship;
(parent: $m, child: $y) isa parentship;
(parent: $f, child: $x) isa parentship;
(parent: $f, child: $y) isa parentship;
$m has gender "female";
$f has gender "male";
$x != $y;
}
rhs
{(sibling1: $x, sibling2: $y) isa siblings;};

$peopleWithSiblingsParentsAreCousins isa inference-rule
lhs
{
(parent: $p, child: $c1) isa parentship;
($p, $p2) isa siblings;
(parent: $p2, child: $c2) isa parentship;
$c1 != $c2;
}
rhs
{(cousin1: $c1, cousin2: $c2) isa cousins;};

$parentsOfSpousesAreInLaws isa inference-rule
lhs
{(spouse1: $x, spouse2: $y) isa marriage;
(parent: $il, child: $y) isa parentship;}
rhs
{(parent-in-law: $il, child-in-law: $x) isa in-laws;};

$parentsOfSpousesAreInLaws2 isa inference-rule
lhs
{(spouse1: $x, spouse2: $y) isa marriage;
(parent: $il, child: $x) isa parentship;}
rhs
{(parent-in-law: $il, child-in-law: $y) isa in-laws;};

#TODO: THIS IS HOW THE RULE SHOULD LOOK LIKE. FOR THE MOMENT I WILL USE TWO RULES INSTEAD
#$parentsOfSpousesAreInLaws isa inference-rule
#lhs
#{($r1: $x, $r2: $y) isa marriage;
#$r1 isa generic-spouse;
#$r2 isa generic-spouse;
#(parent: $il, child: $y) isa parentship;}
#rhs
#{(parent-in-law: $il, child-in-law: $x) isa in-laws;};